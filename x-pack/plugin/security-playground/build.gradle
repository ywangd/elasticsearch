apply plugin: 'elasticsearch.internal-es-plugin'
apply plugin: 'elasticsearch.internal-cluster-test'

esplugin {
  name 'x-pack-security-playground'
  description 'A security playground plugin'
  classname 'org.elasticsearch.xpack.security.playground.SecurityPlaygroundPlugin'
  extendedPlugins = ['x-pack-security']
}

archivesBaseName = 'x-pack-security-playground'

dependencies {
  api project(":client:rest")

  api 'org.hdrhistogram:HdrHistogram:2.1.9'

  compileOnly project(':x-pack:plugin:core')
  compileOnly project(':x-pack:plugin:security')

  testImplementation(testArtifact(project(xpackModule('core'))))
  testImplementation(testArtifact(project(xpackModule('security'))))
  internalClusterTestImplementation(testArtifact(project(xpackModule('core'))))
  internalClusterTestImplementation(testArtifact(project(xpackModule('security'))))
}

tasks.named("bundlePlugin").configure{
  from ('build/classes/java/main/org/elasticsearch/xpack/security/authz/') {
    into 'security/org/elasticsearch/xpack/security/authz'
  }
  from ('build/classes/java/main/org/elasticsearch/xpack/core/security/authz/permission') {
    into 'core/org/elasticsearch/xpack/core/security/authz/permission'
  }
}

tasks.named("thirdPartyAudit").configure {
  enabled = false
}

tasks.named("forbiddenApisMain").configure {
  enabled = false
}

tasks.named("splitPackagesAudit").configure {
  enabled = false
}

tasks.named("test").configure {
  /*
   * We have to disable setting the number of available processors as tests in the same JVM randomize processors and will step on each
   * other if we allow them to set the number of available processors as it's set-once in Netty.
   */
  systemProperty 'es.set.netty.runtime.available.processors', 'false'
}

tasks.named("internalClusterTest").configure {
  /*
   * We have to disable setting the number of available processors as tests in the same JVM randomize processors and will step on each
   * other if we allow them to set the number of available processors as it's set-once in Netty.
   */
  systemProperty 'es.set.netty.runtime.available.processors', 'false'

  /*
   * Some tests in this module set up a lot of transport threads so we reduce the buffer size per transport thread from the 1M default
   * to keep direct memory usage under control.
   */
  systemProperty 'es.transport.buffer.size', '256k'
}
